apply plugin: "java"
apply plugin: "war"
apply plugin: "jetty"
apply plugin: "idea"
apply plugin: "maven"

group = "org.springframework.data"
version = "$sdRestVersion"
archivesBaseName = "spring-data-rest-webmvc"

jettyRun.contextPath = "data"

project.sourceCompatibility = 1.6
project.targetCompatibility = 1.6

configurations.all {
  exclude group: "commons-logging"
  exclude module: "slf4j-log4j12"
  exclude module: "servlet-api"
}

repositories {
  //maven { url "http://repo.springsource.org/libs-snapshot" }
  maven { url "http://repo.springsource.org/libs-milestone" }
  maven { url "http://repo.springsource.org/libs-release" }
}

dependencies {

  // Logging
  compile "org.slf4j:slf4j-api:$slf4jVersion"
  runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
  runtime "ch.qos.logback:logback-classic:$logbackVersion"

  // JPA
  compile "org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final"
  runtime "org.hibernate:hibernate-entitymanager:$hibernateVersion"
  runtime "org.hsqldb:hsqldb:2.2.8"

  // Spring Data
  compile "org.springframework.data:spring-data-commons-core:$sdCommonsVersion"
  compile "org.springframework.data:spring-data-jpa:$sdJpaVersion"

  // Spring Data REST
  compile "org.springframework.data:spring-data-rest-webmvc:$sdRestVersion"

}

task wrapper(type: Wrapper) { gradleVersion = "1.0-rc-1" }

idea {
  project {
    jdkName = "1.6"
    languageLevel = "1.6"
    ipr {
      withXml { xml ->
        xml.node.component.find { it.@name == "VcsDirectoryMappings" }.mapping.@vcs = "Git"
        xml.node.component.find { it.@name == "ProjectRootManager" }.output.@url = "file://\$PROJECT_DIR\$/build"
      }
    }
  }
}